\section{Introduction to Coq}
\subauthor{Jennifer Paykin}{Intel}
\plquote{``The fun keyword turns the file into a party!''}
\begin{abstract}
	Coq is an interactive theorem prover that can be used to develop mechanized proofs of correctness for software systems. It uses dependent types and proof tactics to state and prove theorems about functional programs. This course will introduce Coq and the basics of interactive theorem provers through hands-on exercises and labs based on the Software Foundations curriculum.
\end{abstract}

\subsection{Overview}
\textbf{Formal Methods}: Mathematically rigorous techniques for the specification, development, analysis and verification
of software and hardware systems. For example, type systems, program modeling and static analysis.

\textbf{Formal Verification}: Mahcine-checked proof of the correctness of a software or hardware system with respect
to a certain formal specification or property. For example, automated theorem provers like SMT solvers and interactive
proof assistants that rely on logics like first-order logic (ACL2), higher order logic (Isabelle) or dependent types (Coq, Lean, Agda, etc).

We are interested in two questions: 
\begin{enumerate}
\item Do programs match their specifications?
\item Are proofs about those specifications correct?
\end{enumerate}

Coq was developed by Thiery Coquand based on his Calculus of (Inductive) Constructions. It is mainly maintained by researchers at Inria in
France. Coq is written in OCaml.

Coq theorem proving includes three parts: the first part is the program execution model, which is similar to other functional languages
with dependent types. The second part is the specification which consists of preconditions and postconditions, semantics preservation theorems
and other properties (like in-bounds array access). The third part ties the two together and consists of theorems and ways to prove them.

\subsection{Coq Basics}
We can define an inductive datatype, as is typical in function languages, with the keyword \lstinline{Inductive}.
For example, the natural numbers can be defined as:
\begin{lstlisting}
Inductive nat : Set :=
	| O : nat
	| S (n : nat) : nat.
\end{lstlisting}
We can define functions using \lstinline{Definition}. For example:
\begin{lstlisting}
Definition pred (n : nat) : nat :=
	match n with
	| O => O
	| S n' => n'
	end.
\end{lstlisting}
We can define recursive functions using \lstinline{Fixpoint}. For example:
\begin{lstlisting}
Fixpoint plus (n1 n2 : nat) : nat :=
	match n1 with
	| O => n2
	| S n1' => S (plus n1' n2)
	end.
\end{lstlisting}

We can perform normal-form reductions using the keyword \lstinline{Compute}: 

\lstinline{Compute (S (S O) + S O). (* 2 + 1 = 3 *)}.

As usual, we define the \lstinline{List} datatype, which itself is dependent on a type (a type constructor):
\begin{lstlisting}
Inductive list (A : Type) : Type :=
    | nil : list A
    | cons : A -> list A -> list A.
\end{lstlisting}

In this case, it is possible to make the type parameter implicit using \lstinline{Arguments}: \lstinline|Arguments nil {A}|.

We also define \lstinline{app} and \lstinline{length}:
\begin{lstlisting}
Fixpoint app {X : Type} (l1 l2 : list X) : list X :=
	match l1 with
	| nil => l2
	| cons h t => cons h (app t l2)
	end.

Fixpoint length {X : Type} (l : list X) : nat :=
	match l with
	| nil => 0
	| cons _ l' => S (length l')
	end.
\end{lstlisting}

Of course, like any self-respecting functional language, higher-order functions like \lstinline{map} are suppported and 
encouraged:
\begin{lstlisting}
Fixpoint map {A B : Type} (f : A -> B) (l : list A) : list B :=
	match l with
	| [] => []
	| a :: l' => f a :: map f l'
	end.
\end{lstlisting}

It is also possible to introduce anonymous functions using \lstinline{fun x => ...}.

\subsection{Dependent Types}
Coq's type system is founded on dependent types: types that depend on a value. For example:
\begin{lstlisting}
Inductive Vec (A : Type) : nat -> Type := 
    | vnil : Vec A 0
    | vcons {n : nat} (a : A) (v : Vec A n) : Vec A (S n).
Check vnil. (* forall A, Vec A 0 *)
Check vcons. (* forall A n, A -> Vec A n -> Vec A (S n) *)
\end{lstlisting}

Here too we can make the arguments explicit: \lstinline|Arguments vcons {A n}|.

We can write functions accepting these types:
\begin{lstlisting}
Fixpoint vappend {A : Type} {n1 n2 : nat} (ls1 : Vec A n1) (ls2 : Vec A n2) : Vec A (n1 + n2) :=
	match ls1 with
	| vnil => (* n1=0, n1 + n2 = n2 *) ls2
	| vcons a ls1' => (* n1 = S n1', n1 + n2 = S (n1' + n2) *) vcons a (vappend ls1' ls2)
	end.

Fixpoint to_list {A : Type} {n : nat} (ls : Vec A n) : list A :=
	match ls with
	| vnil => nil
	| vcons a ls' => cons a (to_list ls')
	end.
\end{lstlisting}

\subsection{Programs as Proofs}
Consider the following dependent type:
\begin{lstlisting}
Inductive foo {A : Type} : A -> A -> Type :=
    | foo_same (a : A) : foo a a.
\end{lstlisting}

Since the only term of type \lstinline{foo a b} is a constructor \lstinline{foo_same a}, the existence
of such a term is a \emph{proof} that \lstinline{a = b}. This type exists in the standard library
and is called \lstinline{eq} with its constructor \lstinline{eq_refl}.
We can write hypotheses, which may or may not be inhabited:
\begin{lstlisting}
	Hypothesis some_hyp : foo 5 6.
\end{lstlisting}

We can do pattern matching on the proof and use it to create new proofs:
\begin{lstlisting}
Definition foo_commutativity {A : Set} (a b : A) (pf : foo a b) : foo b a :=
	match pf with
	| foo_same a => foo_same a
	end.
\end{lstlisting}

We can use this to prove facts on our programs:
\begin{lstlisting}
Fixpoint to_list_length {A : Type} {n : nat} (ls : Vec A n)
             : length (to_list ls) = n :=
	match ls with
	| vnil => (* n = 0, to_list ls = nil *) eq_refl
    | vcons a ls' =>
    	match to_list_length ls' with
      	| eq_refl _ => eq_refl _
      	end
    end.
\end{lstlisting}
This is somewhat of a hassle. This is why we have \emph{tactics} and proof mode. 

\subsection{Solutions to Lab 1}
Solution for \lstinline{factorial}:
\begin{lstlisting}
Fixpoint factorial (n:nat) : nat := 
	match n with
	| 0 => 1
	| S n => (n + 1) * (factorial n)
	end.	
\end{lstlisting}

Solution for \lstinline{eqb}:
\begin{lstlisting}
Fixpoint eqb (n m : nat) : bool := 
	match n, m with
	| 0, 0 => true
	| 0, _ => false
	| _, 0 => false
	| (S n), (S m) => eqb n m
	end.
\end{lstlisting}

Solution for binary number exercises:
\begin{lstlisting}
Fixpoint incr (m:bin) : bin := 
	match m with
	| Z => B1 Z
	| B0 n => B1 n
	| B1 n => B0 (incr n) 
	end.

Fixpoint bin_to_nat (m:bin) : nat := 
	match m with
      | Z => 0
      | B0 n => 2 * (bin_to_nat n)
      | B1 n => 2 * (bin_to_nat n) + 1
      end.
\end{lstlisting}

Solution for \lstinline{nonzeros}:
\begin{lstlisting}
Fixpoint nonzeros (l:list nat) : list nat :=
	match l with
	| nil => nil
	| n::l => match n with
			  | 0 => nonzeros l
			  | S n => (S n)::(nonzeros l)
              end
      end.
\end{lstlisting}

Solution for \lstinline{from_list}:
\begin{lstlisting}
Fixpoint from_list {A : Type} (l:list A) : Vec A (length l) := 
	match l with
	| nil => vnil
	| (a::ll) => vcons a (from_list ll)
	end.
\end{lstlisting}

Solution for \lstinline{add_shuffle}:
\begin{lstlisting}
Theorem add_shuffle3 : forall n m p : nat, n + (m + p) = m + (n + p). Proof. 
	intros. rewrite !add_assoc. apply PeanoNat.Nat.add_cancel_r. apply add_comm. 
Qed.
\end{lstlisting}

Solution for \lstinline{double_plus}:
\begin{lstlisting}
Lemma double_plus : forall n, double n = n + n. Proof.
	induction n. auto.  simpl. rewrite IHn. auto.
Qed.
\end{lstlisting}

Solution for \lstinline{binary_commute}:
\begin{lstlisting}
Theorem binary_commute orig : bin_to_nat (incr orig) = S (bin_to_nat orig). Proof.
	induction orig. simpl. reflexivity. simpl.
	rewrite PeanoNat.Nat.add_1_r. reflexivity.
	simpl. rewrite PeanoNat.Nat.add_0_r.
	rewrite IHorig.
	rewrite PeanoNat.Nat.add_0_r.
	rewrite plus_Sn_m.
	apply f_equal.
	rewrite <- add_assoc.
	rewrite PeanoNat.Nat.add_cancel_l.
	rewrite PeanoNat.Nat.add_1_r. reflexivity.
Qed.
\end{lstlisting}

Solution for \lstinline{nat_bin_nat}:
\begin{lstlisting}
Fixpoint nat_to_bin (n:nat) : bin := match n with
	| 0 => Z
	| S n' => incr (nat_to_bin n')
end.

Theorem nat_bin_nat : forall n, bin_to_nat (nat_to_bin n) = n. Proof. 
	intros. induction n. simpl. reflexivity.
	simpl. rewrite  binary_commute. rewrite IHn.
	reflexivity. 
Qed.
\end{lstlisting}

\subsection{Proofs as Programs}

We saw that \lstinline{to_list_length} is a bit of a hassle. We can also write functions as proofs - this is because Coq is a
constructive system. We can write:
\begin{lstlisting}
Definition to_list_length' : forall (A : Type) (n : nat) (ls : Vec A n), length (to_list ls) = n.
Proof.
	intros A n ls.
	induction ls.
	* simpl. (* unfold definitions *)
		apply eq_refl.
	* simpl.
		rewrite IHls. (* transitivity of eq *)
		reflexivity. (* apply eq_refl *)
Qed.
\end{lstlisting}

This example demonstrates a few tactics: \lstinline{intros} creates hypotheses based on \lstinline{forall}s and parameters.
\lstinline{induction l} splits the current goal into one for every constructor of the inductive type of \lstinline{l}, and gives us
the induction hypothesis for each part. It is possible to give custom names for the variables and the IH in each case:
\lstinline{induction l as [ | n' a ls' IH]}. Notice that the nil case has nothing created because it does not have any consisting
parts.

\lstinline{simpl} does $\beta$-reductions (and a bit more). \lstinline{apply l} applies
an existing lemma \lstinline{l}\footnote{we could have also given an explicit proof term here: \lstinline{refine (eq_refl 0)}}. Also,
\lstinline{reflexivity} is the same as \lstinline{apply eq_refl}.
The tactic \lstinline{rewrite l} uses the hypothesis \lstinline{l} which is an equality and replaced occurences of the left-hand
side with the right-hand side. It is possible to rewrite the other way as well: \lstinline{rewrite <- l}.
The \lstinline{unfold} tactic unfolds definitions.

The \lstinline{inversion} tactic uses the injectivity of datatype constructors and tries to perform unification. It usually
contains useless variables that are equal to existing ones, so it is usually followed by the \lstinline{subst} tactic that substitutes
equalities and then eliminates them. 

\lstinline{generalize dependent varname} is the opposite of \lstinline{intro}/\lstinline{intros} - it creates \lstinline{forall}
clauses.

It is possible to focus one goal at a time by using an itemization, which can be done with symbols like \lstinline{+, -, *} (in this example,
\lstinline{*} is used). These can also be nested. Also, if more bullets are needed, repeating these characters consecutively also makes a 
legal bullet. It is also possible to focus a specific subgoal using its number: \lstinline{2: }. A composition of tactics
can be created by using curly braces around them.

Instead of using \lstinline{intros}, we can also use the \lstinline{refine} tactic that accepts a proof term with holes and 
creates goals to fill these holes. We could have replaced the first line of the ``definition'' with \lstinline{refine (fun A n ls => _)}. 
There's also \lstinline{exact} which is like \lstinline{refine} but without holes.
Also, \lstinline{induction} is just \lstinline{Fixpoint} with \lstinline{match}.

After no goals remain, we can write \lstinline{Qed.} to perform some extra checks, construct the proof term and register it.
While this looks like a proof, this does construct an actual term, which can be printed using \lstinline{Print to_list_length'}.
Also, we can use \lstinline{Defined.} instead of \lstinline{Qed.} to preserve some computational properties.

It is possible to use \lstinline{Theorem.} or \lstinline{Lemma.} instead of \lstinline{Definition.} and that would be the same.

\subsection{Inductive Predicates}

It is possible to define custom propositions and predicates using \lstinline{Inductive}:
\begin{lstlisting}
Inductive le : nat -> nat -> Prop :=
	| le_O : forall n, le O n
	| le_S : forall m, n, le m n -> le (S m) (S n) 
\end{lstlisting}

We can use recursively-structured propositions and use them to prove inhabitation of types such as
\lstinline{forall m n : nat, le m n -> le n m -> m = n}. It is possible to prove this lemma using 
a special induction principle: 

\begin{lstlisting}
le_ind : forall P : nat -> nat -> Prop, (forall n : nat, P 0 n) -> 
	(forall n : nat, P 0 n) -> 
	(forall m n : nat, le m n -> P m n -> P (S m) (S n)) -> 
	forall n n0 : nat, le n n0, P n n0}.
\end{lstlisting}

\subsection{Conversion rules}
Coq is based on conversion rules that are used to determine whether two terms are equivalent from
the perspective of CIC, or \emph{convertible}.
These are a few conversion rules: $\beta$-reduction eliminates \lstinline{fun}, $\delta$-reduction replaces a constant
or variable with its definition, $\zeta$-reduction eliminates \lstinline{let}, $\eta$-reduction replaces 
\lstinline{f : forall a: A, B} with \lstinline{fun x : A => f x}. This is similar to the notion of 
\emph{definitional equality}. It is possible to change terms to a convertible term using \lstinline{change (_ -> _)}.

Note that conversion rules are distinct from equality - the notion of two terms representing the same abstract
concept. This is a standard library feature, usually defined as:
\begin{lstlisting}
Inductive eq {A : Type} (x : A) : A -> Prop := eq_refl : eq x x.
\end{lstlisting}

In Coq, all evaluation occurs during type checking. As a result, all Coq functions terminate. Recursive functions
must be \emph{guarded} by a structurally-decreasing argument for termination proofs.

\subsection{Booleans and Propositions}
In Coq, we have two ways of expressing logical claims. One is using Booleans and the other is using propositions. 
\lstinline{bool} is an inductive type from the standard library with two constructors. \lstinline{Prop} is a built-in
type inhabited by types such as \lstinline{Inductive True : Prop := true_cons.} which is inhabited and
\lstinline{Inductive False : Prop := .} which is uninhabited. A \lstinline{Prop} is ``decided'' by checking
if it is inhabited. A \lstinline{bool} is decided by checking if it reduces to \lstinline{true} or
\lstinline{false}.

The \lstinline{bool} type is, therefore, decidable, unlike the \lstinline{Prop} type. However, only \lstinline{Prop}
types can be used for rewriting equalities. It is possible to define lemmas asserting that certain properties
are decidable:
\begin{lstlisting}
Lemma string_eqb_dec : forall n m : string, n = m \/ n <> m.
\end{lstlisting}

This may seem like a disadvantage of \lstinline{Prop}, but actually that just makes it more expressive. For example,
there does not exist a function \lstinline{nat -> bool} that returns \lstinline{true} if and only if 
``the n\textsuperscript{th} Turing machine halts''. However, there does exist such a function \lstinline{nat -> Prop}.

\todo{Write about functional extensionality}

\subsection{Coq Automation}

